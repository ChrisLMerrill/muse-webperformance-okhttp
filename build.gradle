apply plugin: 'java'
apply plugin: 'maven'

version = 0.3
group = 'com.webperformance.muse'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'org.musetest:muse-core:0.15'
    testCompile 'org.musetest:muse-commandline:0.15'  // only needed for debugging via command-line execution

    // Alert: These are compile-time dependencies. They do not need to be included with the extension packaging
    // because they are included by the muse-selenium module. But if/when the muse-selenium module is moved from
    // the core into a Muse extension, then these will need to be provided.
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'

    testCompile 'junit:junit:4.12'
}

// jar the sources
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// jar the javadocs
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// produce the sources and javadoc artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

// declare which packages Muse should scan for extensions
jar {
    manifest {
        attributes 'muse-package' : 'com.webperformance.muse.okhttp'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}